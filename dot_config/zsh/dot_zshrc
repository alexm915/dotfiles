# ==================== XDG & Cache Setup ====================
export XDG_CONFIG_HOME="${HOME}/.config"
export XDG_CACHE_HOME="${HOME}/.cache"
export ZSH_CACHE_DIR="${XDG_CACHE_HOME}/zsh"

# 确保缓存目录存在
[[ -d "$ZSH_CACHE_DIR" ]] || mkdir -p "$ZSH_CACHE_DIR"
# 历史文件路径
export HISTFILE="${ZSH_CACHE_DIR}/history"
export HISTSIZE=10000
export SAVEHIST=1000
# 补全缓存路径
export ZSH_COMPDUMP="${ZSH_CACHE_DIR}/zcompdump-${HOST/.*/}-${ZSH_VERSION}"


#==================== Environment & PATH ====================
export LANG=en_US.UTF-8
export EDITOR="nvim"
export VISUAL="nvim"
#export CONAN_HOME="${XDG_CONFIG_HOME}/conan2"


# 使用typeset -U 自动去重
typeset -U path
path=(
    $HOME/bin
    $HOME/.local/bin
    $HOME/.cargo/bin
    /usr/local/bin
    $path
)
export PATH


# ==================== Zim Setup ====================
ZIM_HOME="${XDG_CACHE_HOME}/zim"
ZIM_CONFIG_FILE="${ZDOTDIR:-${HOME}}/.zimrc"

# 告诉Zim把dump文件放到cache目录
zstyle ':zim:completion' dump-file "$ZSH_COMPDUMP"

#--Auto Download Modules/Plugins
if [[ ! -e ${ZIM_HOME}/zimfw.zsh ]]; then
    curl -fsSL --create-dirs -o ${ZIM_HOME}/zimfw.zsh \
        https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
fi
# --Auto Install Modules/Plugins
if [[ ! ${ZIM_HOME}/init.zsh -nt ${ZIM_CONFIG_FILE} ]]; then
    source ${ZIM_HOME}/zimfw.zsh init
fi
#--Initialize Zim
source ${ZIM_HOME}/init.zsh



# ==================== Alias ====================

# file system
alias ll='ls -lah'
alias mkdir='mkdir -p'
alias rm='echo "Use tp (trash-put) instead."; false'
alias tp="trash-put"

# tools
alias vim='nvim'
alias vi='nvim'
alias cz='chezmoi'
alias f='fastfetch'
alias sz="exec zsh"



# ==================== Functions ====================

# 使用tm创建tmuxsession
tm() {
    if [ -z "$1" ]; then
        tmux attach || tmux new-session
    else
        tmux attach -t "$1" || tmux new-session -s "$1"
    fi
}

# ctrl+g打开lazygit
lazygit_widget(){
    lazygit
    zle reset-prompt
}
zle -N lazygit_widget
bindkey '^g' lazygit_widget



# ==================== Vim Mode ===================
bindkey -v
bindkey -M viins 'jk' vi-cmd-mode

function zle-keymap-select {
    if [[ $KEYMAP == vicmd ]]; then
        echo -ne '\e[1 q'  # Block
    else
        echo -ne '\e[5 q'  # Beam
    fi
}
zle -N zle-keymap-select
echo -ne '\e[5 q' # Init cursor

KEYTIMEOUT=20
